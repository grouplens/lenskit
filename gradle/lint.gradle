logger.lifecycle 'Enabling lint support in LensKit build'

ext.lintMode = 'disabled'
if (travis.isReleaseBuild()) {
    logger.lifecycle 'Travis build on release branch'
    ext.lintMode = 'enabled'
}
switch (getConfigProperty('lint', 'false').toLowerCase()) {
    case 'true':
        logger.info 'running in lint mode'
        ext.lintMode = 'enabled'
        break
    case 'preview':
        logger.info 'running in lint preview mode'
        ext.lintMode = 'preview'
        break
}

if (lintMode != 'disabled') {
    allprojects {
        if (name != 'lenskit-integration-tests') {
            apply plugin: 'jacoco'
            jacoco {
                toolVersion = '0.7.0.201403182114'
            }
        }
    }

    task jacocoMergedReport(type: JacocoReport) {
        for (prj in subprojects) {
            dependsOn prj.test
            sourceSets prj.sourceSets.test
            if (prj.sourceSets.test.allSource.files.isEmpty()) {
                logger.info '{} has no tests', prj.name
            } else {
                logger.info 'aggregating tests from {}', prj.name
                executionData prj.test
            }
        }
        reports {
            csv.enabled = false
            xml.enabled = false
            html.destination = "$reporting.baseDir/jacoco"
        }
    }

    report.dependsOn jacocoMergedReport
}

sonarqube {
    properties {
        def branch = travis.branch
        def m = branch != null ? branch =~ /release\/(.*)/ : null
        if (m) {
            def bk = m.group(1)
            property "sonar.projectKey", "$project.group:$project.name-$bk"
        }
        property "sonar.host.url", "https://sonar.cs.txstate.edu"
        property "sonar.sourceEncoding", "UTF-8"
        if (System.getenv("SONAR_USER")) {
            logger.lifecycle("using Sonar user {}", System.getenv("SONAR_USER"))
            property "sonar.login", System.getenv("SONAR_USER")
            property "sonar.password", System.getenv("SONAR_PASSWORD")
        }
        if (lintMode == 'preview') {
            logger.info 'configuring Sonar for preview mode'
            property "sonar.analysis.mode", "preview"
            property "sonar.issuesReport.html.enable", "true"
        }
    }
}

if (lintMode == 'enabled') {
    ciPublish.dependsOn 'sonarqube'
}
